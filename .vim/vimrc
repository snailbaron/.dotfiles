if empty(glob('~/.vim/autoload/plug.vim'))
    silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
        \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
    autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin('~/.vim/plugged')

Plug 'Xuyuanp/nerdtree-git-plugin'
Plug 'christoomey/vim-tmux-navigator'
Plug 'gerw/vim-HiLinkTrace'
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
Plug 'junegunn/fzf.vim'
Plug 'junegunn/vim-plug'
Plug 'majutsushi/tagbar'
Plug 'pboettch/vim-cmake-syntax'
Plug 'pdavydov108/vim-lsp-cquery'
Plug 'prabirshrestha/async.vim'
Plug 'prabirshrestha/asyncomplete-lsp.vim'
Plug 'prabirshrestha/asyncomplete.vim'
Plug 'prabirshrestha/vim-lsp'
Plug 'scrooloose/nerdtree'
Plug 'tmux-plugins/vim-tmux'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-dispatch'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-obsession'
Plug 'tpope/vim-vinegar'
Plug 'vim-airline/vim-airline'
Plug 'zchee/vim-flatbuffers'

call plug#end()

filetype indent off
filetype plugin on

syntax enable

" Create highlight group for improper whitespace usage
autocmd ColorScheme * highlight ExtraWhitespace ctermbg=5 guibg=red

colorscheme snailbaron

set nobackup
set writebackup

set tabstop=4
set shiftwidth=4
set expandtab

set nocindent
set nosmartindent
set autoindent
set backspace=indent,eol,start
set encoding=utf-8
set incsearch
set wildmenu
if &listchars ==# 'eol:$'
    set listchars=tab:>\ ,trail:-,extends:>,precedes:<,nbsp:+
endif
set formatoptions+=j
set autoread
set history=1000

set number
set hlsearch

set splitright
set splitbelow

set completeopt=menu,menuone,preview,noselect,noinsert

" If cursor is on first or last line of the window, scroll to put current line
" in the middle.
function! CenterOnCursorIfOnEdge()
    let cursor_line = winline()
    if cursor_line == 1 || cursor_line == winheight(0)
        normal! zz
    endif
endfunction

nnoremap <silent> <C-]>:call CenterOnCursorIfOnEdge()<CR>
nnoremap <silent> n n:call CenterOnCursorIfOnEdge()<CR>

" Enable mouse support, and make it play nice with tmux
set mouse=a
set ttymouse=xterm2

"set timeoutlen=0 ttimeoutlen=0
set timeoutlen=1000 ttimeoutlen=0

" English, do you speak it?
if has('win32')
    language english
else
    language en_US.UTF-8
endif

inoremap <S-Tab> <C-d>

" Process new buffer creation
autocmd BufNewFile,BufRead * call OnBufferOpen()

autocmd FileType html,xml,fbs,yaml setlocal tabstop=2 shiftwidth=2

""" Colors and highlighting

" Color column 81 with light gray
set colorcolumn=81
highlight ColorColumn ctermbg=DarkGray

" Indicate current line with deep black
set cursorline
highlight CursorLine cterm=NONE ctermbg=Black

" GNU Global related settings
let GtagsCscope_Auto_Load = 1
let GtagsCscope_Auto_Map = 1
let GtagsCscope_Quiet = 1
set cscopetag

"
" netrw
"

let g:netrw_banner = 0
let g:netrw_sort_sequence = "[\/]$"
let g:netrw_liststyle = 3

"
" NERDTree
"

let g:NERDTreeMinimalUI = 1
let g:NERDTreeWinSize = 61
let NERDTreeAutoDeleteBuffer = 1

let g:NERDTreeMapJumpNextSibling = ''

nnoremap <silent> <F4> :NERDTreeToggle<CR>
noremap <silent> <F1> :NERDTreeFocus<CR>

autocmd StdinReadPre * let s:std_in=1
autocmd VimEnter * if argc() == 0 && !exists("s:std_in") | NERDTree | endif

" vim-dispatch
nnoremap <silent> <F7> :Dispatch<CR>

" fzf
nnoremap <C-P> :FZF<CR>

" vim-lsp
if executable('ccls')
    au User lsp_setup call lsp#register_server({
    \   'name': 'ccls',
    \   'cmd': {server_info->['ccls']},
    \   'root_uri': {
    \       server_info->lsp#utils#path_to_uri(
    \           lsp#utils#find_nearest_parent_file_directory(
    \               lsp#utils#get_buffer_path(),
    \               'compile_commands.json'))},
    \   'initialization_options': {
    \       'cacheDirectory': expand('~') . '/.cache/ccls/'
    \   },
    \   'whitelist': ['c', 'cpp', 'objc', 'objcpp', 'cc'],
    \ })
elseif executable('cquery')
    au User lsp_setup call lsp#register_server({
    \   'name': 'cquery',
    \   'cmd': {server_info->['cquery']},
    \   'root_uri': {
    \       server_info->lsp#utils#path_to_uri(
    \       lsp#utils#find_nearest_parent_file_directory(
    \           lsp#utils#get_buffer_path(),
    \           'compile_commands.json'))
    \    },
    \   'initialization_options': {
    \       'cacheDirectory': expand('~') . '/.cache/cquery/'
    \   },
    \   'whitelist': ['c', 'cpp', 'objc', 'objcpp', 'cc'],
    \   })
endif

nnoremap <silent> gd :LspDefinition<CR>

let g:lsp_signs_enabled = 1
let g:lsp_diagnostics_echo_cursor = 1
let g:lsp_signs_error = {'text': '>>'}
let g:lsp_signs_warning = {'text': '--'}
let g:lsp_signs_hint = {'text': '**'}

let g:asyncomplete_auto_popup = 1
"let g:asyncomplete_smart_completion = 1
"let g:asyncomplete_remove_duplicates = 1

"let g:lsp_log_verbose = 1
"let g:lsp_log_file = expand('~/vim-lsp.log')
"let g:asyncomplete_log_file = expand('~/asyncomplete.log')

autocmd! CompleteDone * if pumvisible() == 0 | pclose | endif

"
" Termdebug mappings
"
"nmap <silent> <F7> :Step<CR>
"nmap <silent> <F8> :Over<CR>
"nmap <silent> <F9> :Break<CR>
"nmap <silent> <S-F9> :Delete<CR>
"nmap <silent> <F10> :Delete<CR>
"nmap <silent> <S-F7> :Finish<CR>

"
" Function to run each time when opening a new buffer
"

function! OnBufferOpen()
    " Highlight unnecessary whitespace
    if &filetype == "cpp"
        match ExtraWhitespace /\s\+$\|\t/
    else
        match ExtraWhitespace /\s\+$/
    endif
endfunction

"
" tagbar mappings
"
nnoremap <F8> :TagbarToggle<CR>

"
" vim-tmux-navigator mappings
"

let g:tmux_navigator_no_mappings = 1

nnoremap <silent> <C-h> :TmuxNavigateLeft<cr>
nnoremap <silent> <C-j> :TmuxNavigateDown<cr>
nnoremap <silent> <C-k> :TmuxNavigateUp<cr>
nnoremap <silent> <C-l> :TmuxNavigateRight<cr>
nnoremap <silent> <C-\> :TmuxNavigatePrevious<cr>

if v:version > 800 || v:version == 800 && has("patch587")
    tnoremap <silent> <C-h> <C-w>:TmuxNavigateLeft<cr>
    tnoremap <silent> <C-j> <C-w>:TmuxNavigateDown<cr>
    tnoremap <silent> <C-k> <C-w>:TmuxNavigateUp<cr>
    tnoremap <silent> <C-l> <C-w>:TmuxNavigateRight<cr>
    "tnoremap <silent> <C-\> <C-w>:TmuxNavigatePrevious<cr>
endif

"
" Seamless resize in Vim/tmux
"

let g:tmux_resize_vertical_step = 3
let g:tmux_resize_horizontal_step = 5

function! s:CanMove(vertical, screenpos)
    if a:vertical
        let windowTop = a:screenpos - winline() + 1
        let windowBottom = windowTop + winheight(0) + 2
        return [windowTop > 1, windowBottom < &lines]
    else
        let windowLeft = a:screenpos - wincol() + 1
        let windowRight = windowLeft + winwidth(0) - 1
        return [windowLeft > 1, windowRight < &columns]
    endif
endfunction

function! s:IsLastTmuxPane(forwardDirection)
    if a:forwardDirection == 'l'
        let pane_right = system("tmux display -p '#{pane_right}'")
        let window_width = system("tmux display -p '#{window_width}'")
        return pane_right + 1 == window_width
    elseif a:forwardDirection == 'j'
        let pane_bottom = system("tmux display -p '#{pane_bottom}'")
        let window_height = system("tmux display -p '#{window_height}'")
        return pane_bottom + 1 == window_height
    endif
endfunction

function! TmuxResize(positive, vertical, tmuxDirection, step, forwardDirection, backDirection)
    let position = [screencol(), screenrow()]
    let canMove = s:CanMove(a:vertical, position[a:vertical])
    let firstWindow = !canMove[0]
    let lastWindow = !canMove[1]

    if $TMUX != '' && lastWindow && (firstWindow || !s:IsLastTmuxPane(a:forwardDirection))
        silent call system('tmux resize-pane -' . a:tmuxDirection . ' ' . a:step)
        "redraw!
    else
        let sign = xor(a:positive, lastWindow) ? '+' : '-'
        let prefix = a:vertical ? '' : 'vertical '
        execute prefix . 'resize ' . sign . a:step
    endif
    "redraw!
endfunction

command! TmuxResizeLeft :call TmuxResize(0, 0, 'L', g:tmux_resize_horizontal_step, 'l', 'h')
command! TmuxResizeRight :call TmuxResize(1, 0, 'R', g:tmux_resize_horizontal_step, 'l', 'h')
command! TmuxResizeDown :call TmuxResize(1, 1, 'D', g:tmux_resize_vertical_step, 'j', 'k')
command! TmuxResizeUp :call TmuxResize(0, 1, 'U', g:tmux_resize_vertical_step, 'j', 'k')

nnoremap <silent> <M-h> :TmuxResizeLeft<CR>
nnoremap <silent> <M-l> :TmuxResizeRight<CR>
nnoremap <silent> <M-j> :TmuxResizeDown<CR>
nnoremap <silent> <M-k> :TmuxResizeUp<CR>

nnoremap <silent> <Esc>h :TmuxResizeLeft<CR>
nnoremap <silent> <Esc>l :TmuxResizeRight<CR>
nnoremap <silent> <Esc>j :TmuxResizeDown<CR>
nnoremap <silent> <Esc>k :TmuxResizeUp<CR>

if v:version > 800 || v:version == 800 && has("patch587")
    tnoremap <silent> <M-h> <C-w>:TmuxResizeLeft<CR>
    tnoremap <silent> <M-l> <C-w>:TmuxResizeRight<CR>
    tnoremap <silent> <M-j> <C-w>:TmuxResizeDown<CR>
    tnoremap <silent> <M-k> <C-w>:TmuxResizeUp<CR>

    tnoremap <silent> <Esc>h <C-w>:TmuxResizeLeft<CR>
    tnoremap <silent> <Esc>l <C-w>:TmuxResizeRight<CR>
    tnoremap <silent> <Esc>j <C-w>:TmuxResizeDown<CR>
    tnoremap <silent> <Esc>k <C-w>:TmuxResizeUp<CR>
endif

autocmd BufWinEnter,WinEnter term://* startinsert
autocmd BufLeave term://* stopinsert

packloadall
silent! helptags ALL
