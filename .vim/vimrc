call plug#begin('~/.vim/plugged')

Plug 'PProvost/vim-ps1'
Plug 'Valloric/YouCompleteMe', {'do': './install.py --clang-completer'}
Plug 'christoomey/vim-tmux-navigator'
Plug 'scrooloose/nerdcommenter'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-obsession'
Plug 'tpope/vim-vinegar'
Plug 'vim-airline/vim-airline'
Plug 'vim-scripts/Conque-GDB'

call plug#end()

filetype plugin indent off

syntax enable

" Create highlight group for improper whitespace usage
autocmd ColorScheme * highlight ExtraWhitespace ctermbg=5 guibg=red

colorscheme desert

set nobackup
set writebackup

set tabstop=4
set shiftwidth=4
set expandtab

set nocindent
set nosmartindent
set autoindent
set backspace=indent,eol,start
set encoding=utf-8

set number
set hlsearch

set splitright
set splitbelow

" Enable mouse support, and make it play nice with tmux
set mouse=a
set ttymouse=xterm2

set timeoutlen=0 ttimeoutlen=0

" English, do you speak it?
if has('win32')
    language english
else
    language en_US.UTF-8
endif

inoremap <S-Tab> <C-d>

" Process new buffer creation
autocmd BufNewFile,BufRead * call OnBufferOpen()

""" Colors and highlighting

" Color column 81 with light gray
set colorcolumn=81
highlight ColorColumn ctermbg=magenta

" Indicate current line with deep black
set cursorline
highlight CursorLine cterm=NONE ctermbg=black guibg=black

"
" netrw
"

let g:netrw_banner = 0
let g:netrw_sort_sequence = "[\/]$"

"
" YouCompleteMe
"
" YCM options are documented here:
"     https://github.com/Valloric/YouCompleteMe#options
"

" Fallback global YCM configuration file
let g:ycm_global_ycm_extra_conf = '~/.global_ycm_extra_conf.py'

" Do not use TAB for selection from completion list
let g:ycm_key_list_select_completion = ['<Down>']
let g:ycm_key_list_previous_completion = ['<Up>']

let g:ycm_warning_symbol = '~>'

" Do not ask if it is safe to load a configuration file. WARNING: allows
" execution of malicious .ycm_extra_conf.py.
let g:ycm_confirm_extra_conf = 0

" Highlight YCM errors with less acid color
highlight YcmErrorSection ctermbg=5

" Mappings for GoTo commands
nmap <F12> :YcmCompleter GoTo<CR>
nmap <C-S-g> :YcmCompleter GoToInclude<CR>

"
" Function to run each time when opening a new buffer
"

function! OnBufferOpen()
    " Highlight unnecessary whitespace
    if &filetype == "cpp"
        match ExtraWhitespace /\s\+$\|\t/
    else
        match ExtraWhitespace /\s\+$/
    endif
endfunction

"
" Seamless resize in Vim/tmux
"

let g:tmux_resize_vertical_step = 3
let g:tmux_resize_horizontal_step = 5

function! s:CanMove(direction)
    let nr = winnr()
    execute 'wincmd ' . a:direction
    let success = (nr != winnr())
    if success
        execute nr . 'wincmd w'
    endif
    return success
endfunction

function! s:IsLastTmuxPane(forwardDirection)
    if a:forwardDirection == 'l'
        let pane_right = system("tmux display -p '#{pane_right}'")
        let window_width = system("tmux display -p '#{window_width}'")
        return pane_right + 1 == window_width
    elseif a:forwardDirection == 'j'
        let pane_bottom = system("tmux display -p '#{pane_bottom}'")
        let window_height = system("tmux display -p '#{window_height}'")
        return pane_bottom + 1 == window_height
    endif
endfunction

function! s:TmuxResize(positive, tmuxDirection, step, prefix, forwardDirection, backDirection)
    let lastWindow = !s:CanMove(a:forwardDirection)
    if lastWindow && (!s:CanMove(a:backDirection) || !s:IsLastTmuxPane(a:forwardDirection))
        silent call system('tmux resize-pane -' . a:tmuxDirection . ' ' . a:step)
        "redraw!
    else
        let sign = xor(a:positive, lastWindow) ? '+' : '-'
        execute a:prefix . 'resize ' . sign . a:step
    endif
    "redraw!
endfunction

command! TmuxResizeLeft call s:TmuxResize(0, 'L', g:tmux_resize_horizontal_step, 'vertical ', 'l', 'h')
command! TmuxResizeRight call s:TmuxResize(1, 'R', g:tmux_resize_horizontal_step, 'vertical ', 'l', 'h')
command! TmuxResizeDown call s:TmuxResize(1, 'D', g:tmux_resize_vertical_step, '', 'j', 'k')
command! TmuxResizeUp call s:TmuxResize(0, 'U', g:tmux_resize_vertical_step, '', 'j', 'k')

nnoremap <M-h> :TmuxResizeLeft<CR>
nnoremap <M-l> :TmuxResizeRight<CR>
nnoremap <M-j> :TmuxResizeDown<CR>
nnoremap <M-k> :TmuxResizeUp<CR>

nnoremap <Esc>h :TmuxResizeLeft<CR>
nnoremap <Esc>l :TmuxResizeRight<CR>
nnoremap <Esc>j :TmuxResizeDown<CR>
nnoremap <Esc>k :TmuxResizeUp<CR>
