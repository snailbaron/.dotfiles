execute pathogen#infect()

syntax enable

" Create highlight group for improper whitespace usage
autocmd ColorScheme * highlight ExtraWhitespace ctermbg=5 guibg=red

colorscheme desert

set nobackup
set writebackup

set tabstop=4
set shiftwidth=4
set expandtab

set nocindent
set nosmartindent
set autoindent
set backspace=indent,eol,start
set encoding=utf-8

set number
set hlsearch

set timeoutlen=0 ttimeoutlen=0

" Highlight whitespaces in unusual places
match ExtraWhitespace /\s\+$/

" English, do you speak it?
if has('win32')
    language english
else
    language en_US.UTF-8
endif

inoremap <S-Tab> <C-d>

autocmd FileType make set noexpandtab

""" Colors and highlighting

" Color column 81 with light gray
set colorcolumn=81
highlight ColorColumn ctermbg=magenta

" Indicate current line with deep black
set cursorline
highlight CursorLine cterm=NONE ctermbg=black guibg=black

" NERDTree
nmap <F8> :NERDTreeToggle<CR>

"
" YouCompleteMe
"

" Use global YCM configuration file
let g:ycm_global_ycm_extra_conf = '~/.global_ycm_extra_conf.py'

" Do not use TAB for selection from completion list
let g:ycm_key_list_select_completion = ['<Down>']
let g:ycm_key_list_previous_completion = ['<Up>']

" Highlight YCM errors with less acid color
highlight YcmErrorSection ctermbg=5

" Mappings for GoTo commands
nmap <F12> :YcmCompleter GoTo<CR>
nmap <C-S-g> :YcmCompleter GoToInclude<CR>

"
" Resize current window like tmux: move current window's right/bottom side,
" unless we are in a rightmost/bottommost window; in that case, resize the one
" on the left/top.
"

function! CanMoveToWindow(direction)
    let canMove = 1

    let myWindow = winnr()
    execute 'wincmd ' . a:direction
    if winnr() == myWindow
        let canMove = 0
    endif
    execute myWindow . 'wincmd w'

    return canMove
endfunction

function! ResizeWindowRight()
    if CanMoveToWindow('l')
        execute '5wincmd>'
    else
        execute '5wincmd<'
    endif
endfunction

function! ResizeWindowLeft()
    if CanMoveToWindow('h')
        execute '5wincmd>'
    else
        execute '5wincmd<'
    endif
endfunction

function! ResizeWindowUp()
    if CanMoveToWindow('k')
        execute '3wincmd+'
    else
        execute '3wincmd-'
    endif
endfunction

function! ResizeWindowDown()
    if CanMoveToWindow('j')
        execute '3wincmd+'
    else
        execute '3wincmd-'
    endif
endfunction

nnoremap <silent> <Esc>h :call ResizeWindowLeft()<CR>
nnoremap <silent> <Esc>j :call ResizeWindowDown()<CR>
nnoremap <silent> <Esc>k :call ResizeWindowUp()<CR>
nnoremap <silent> <Esc>l :call ResizeWindowRight()<CR>

nnoremap <silent> <M-h> :call ResizeWindowLeft()<CR>
nnoremap <silent> <M-j> :call ResizeWindowDown()<CR>
nnoremap <silent> <M-k> :call ResizeWindowUp()<CR>
nnoremap <silent> <M-l> :call ResizeWindowRight()<CR>

